/* 
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
*/
// Include SHA512 helper functions
include "SHA512-common.saw"; 

// Include rewrite rules
include "goal-rewrites.saw";

// Verify the `EVP_SHA_INIT` C function satisfies the `EVP_sha_init_spec`
// specification
crucible_llvm_verify m EVP_SHA_INIT [] true EVP_sha_init_spec (w4_unint_yices []);

// Verify the `EVP_DigestInit` C function satisfies the `EVP_DigestInit_spec`
// specification
crucible_llvm_verify m "EVP_DigestInit"
  [ OPENSSL_malloc_init_ov
  , OPENSSL_free_null_ov
  ]
  true
  EVP_DigestInit_array_spec
  (do {
    goal_eval_unint [];
    w4_unint_z3 [];
  });

crucible_llvm_verify m "EVP_DigestUpdate"
  [sha512_block_data_order_array_ov]
  true
  EVP_DigestUpdate_array_spec
  (do {
    goal_eval_unint ["processBlocks", "processBlock_Common"];
    simplify (addsimps [processBlocks_0_1_thm] empty_ss);
    simplify (addsimps [arrayRangeEq_arrayRangeLookup_eq_thm, arrayCopy_zero_thm] empty_ss);
    simplify (addsimps append_ite_thms empty_ss);
    goal_eval_unint ["processBlocks", "processBlock_Common"];
    print_goal;
    w4_unint_z3 ["processBlocks", "processBlock_Common"];
  });

let verify_final_with_length withLength = do {
  print (str_concat "Verifying EVP_DigestFinal withLength=" (show withLength));
  crucible_llvm_verify m "EVP_DigestFinal"
    [ sha512_block_data_order_array_ov
    , OPENSSL_free_nonnull_ov
    , OPENSSL_cleanse_ov
    ]
    true
    (EVP_DigestFinal_array_spec true)
    (do {
      goal_eval_unint ["processBlock_Common"];
      simplify (addsimps [arrayUpdate_arrayCopy_thm, arraySet_zero_thm] empty_ss);
      simplify (addsimps append_ite_thms empty_ss);
      goal_eval_unint ["processBlock_Common"];
      print_goal;
      w4_unint_z3 ["processBlock_Common"];
    });
};
for [false, true] verify_final_with_length;

