/* 
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
*/


enable_experimental;

// Disable debug intrinsics to avoid https://github.com/GaloisInc/crucible/issues/778
disable_debug_intrinsics;

// Load LLVM bytecode
m <- llvm_load_module "../../build/llvm_x86/crypto/crypto_test.bc";

// Include SHA512 common specifications
include "SHA512-common-specs.saw";

////////////////////////////////////////////////////////////////////////////////
// Proof commands

llvm_verify m "EVP_MD_pctx_ops_init" [] true EVP_MD_pctx_ops_init_spec (w4_unint_z3 []);
EVP_MD_pctx_ops_ov <- llvm_unsafe_assume_spec
  m
  "CRYPTO_once"
  EVP_MD_pctx_ops_spec;

// Verify the block data function assembly satisfies the
// bounded `sha512_block_data_order_spec` specification

enable_what4_hash_consing;

sha512_block_data_order_ov <- llvm_verify_x86 m "../../build/x86/crypto/crypto_test" "sha512_block_data_order"
  [ ("K512", 5120) // Initializes the global for round constants, called K, at a size of 5120 bytes
  ]
  true
  sha512_block_data_order_spec
  (do {
    simplify (cryptol_ss ());
    simplify (addsimps thms empty_ss);
    simplify (addsimp concat_assoc_thm empty_ss);
    goal_eval_unint ["S0", "S1", "s0", "s1", "Ch"];
    simplify (addsimps thms empty_ss);
    w4_unint_z3 ["S0", "S1", "s0", "s1", "Ch"];
  });

// Verify the block data function assembly satisfies the
// unbounded `sha512_block_data_order_array_spec` specification

enable_what4_eval;
enable_x86_what4_hash_consing;

sha512_block_data_order_array_ov <- llvm_verify_fixpoint_x86 m "../../build/x86/crypto/crypto_test" "sha512_block_data_order"
  [ ("K512", 5120) // Initializes the global for round constants, called K, at a size of 5120 bytes
  ]
  true
  {{ processBlocksLoop }}
  sha512_block_data_order_array_spec
  (do {
    simplify (cryptol_ss ());
    simplify (addsimps thms empty_ss);
    simplify (addsimp concat_assoc_thm empty_ss);
    goal_eval_unint ["processBlocksLoop", "S0", "S1", "s0", "s1", "Ch"];
    simplify (addsimps thms empty_ss);
    w4_unint_z3 ["processBlocksLoop", "S0", "S1", "s0", "s1", "Ch"];
  });

disable_what4_eval;
disable_x86_what4_hash_consing;
