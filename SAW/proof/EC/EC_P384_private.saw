/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
*/

let limb_length = 64;
let p384_felem_limbs = 6;
let wsize = 5;
let numWindows = 77;
let tsize = 16;

let {{ ia32cap = [0xffffffff, 0xffffffff, 0xffffffff, 0xffffffff] : [4][32] }};

let p384_select_point_spec = do {
  outarg_ptr <- crucible_alloc (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  id <- crucible_fresh_var "id" (llvm_int 64);
  (points, points_ptr) <- ptr_to_fresh_readonly "points" (llvm_array tsize (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length))));
 
  crucible_execute_func [outarg_ptr, (crucible_term id), points_ptr, (crucible_term {{`tsize}})];

  crucible_points_to outarg_ptr (crucible_term {{ select_point_ct id points }});
};


let p384_mul_scalar_rwnaf_spec = do {
  rnaf_ptr <- crucible_alloc (llvm_array numWindows (llvm_int 16));
  (scalar, scalar_ptr) <- ptr_to_fresh_readonly "scalar" i384;
 
  crucible_execute_func [rnaf_ptr, scalar_ptr];

  crucible_points_to rnaf_ptr (crucible_term {{ mul_scalar_rwnaf scalar}});
};


let p384_point_mul_spec = do {

  res_ptr <- crucible_alloc (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  table_ptr <- crucible_alloc (llvm_array tsize (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length))));
  p <- crucible_fresh_var "p" (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  crucible_points_to (crucible_elem table_ptr 0) (crucible_term p);
  (scalar, scalar_ptr) <- ptr_to_fresh_readonly "p_scalar" i384;

  crucible_execute_func [res_ptr, table_ptr, scalar_ptr];

  crucible_points_to res_ptr (crucible_term {{ point_mul p scalar }});
};

let p384_point_mul_spec = do {

  global_alloc_init "OPENSSL_ia32cap_P" {{ ia32cap }};

  group_ptr <- crucible_alloc_readonly (llvm_struct "struct.ec_group_st");
  r_ptr <- crucible_alloc (llvm_struct "struct.EC_RAW_POINT");
  p_ptr <- crucible_alloc_readonly (llvm_struct "struct.EC_RAW_POINT");
  p <- points_to_fresh_EC_RAW_POINT p_ptr;
  (scalar, scalar_ptr) <- ptr_to_fresh_readonly "p_scalar" i384;

  crucible_execute_func [group_ptr, r_ptr, p_ptr, scalar_ptr];

  global_points_to "OPENSSL_ia32cap_P" {{ ia32cap }};

  points_to_EC_RAW_POINT r_ptr 
    {{ point_mul_generic p scalar }};
};


let p384_pre_comp_table_spec = do {
  
  table_ptr <- crucible_alloc (llvm_array tsize (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length))));
  p <- crucible_fresh_var "p" (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  crucible_points_to (crucible_elem table_ptr 0) (crucible_term p);
  tmp_ptr <- crucible_alloc (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));

  crucible_execute_func [table_ptr, tmp_ptr];

  crucible_points_to table_ptr (crucible_term {{pre_comp_table p}});
};

let conditional_subtract_if_even_ct_spec = do {
  
  (res, res_ptr) <- ptr_to_fresh "res" (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  (p, p_ptr) <- ptr_to_fresh_readonly "p" (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  tmp_ptr <- crucible_alloc (llvm_array 3 (llvm_array p384_felem_limbs (llvm_int limb_length)));
  (scalar, scalar_ptr) <- ptr_to_fresh_readonly "p_scalar" i384;

  crucible_execute_func [res_ptr, p_ptr, scalar_ptr, tmp_ptr];

  crucible_points_to res_ptr (crucible_term {{conditional_subtract_if_even_ct res scalar p}});
};

p384_select_point_ov <- llvm_verify m
  "p384_select_point" [p384_felem_cmovznz_same_r_ov] true
  p384_select_point_spec (w4_unint_z3 []);


mul_scalar_rwnaf_1_equiv <- prove_folding_theorem {{ \a -> (mul_scalar_rwnaf_1 a) == (mul_scalar_rwnaf_ct a) }};

mul_scalar_rwnaf_equiv <- prove_folding_theorem {{ \a -> (mul_scalar_rwnaf a) == (mul_scalar_rwnaf_1 a) }};


p384_mul_scalar_rwnaf_ov <- llvm_verify m
  "p384_felem_mul_scalar_rwnaf" 
  [p384_get_bit_ov] 
  true
  p384_mul_scalar_rwnaf_spec
  (do{
  goal_eval_unint["scalar_get_bit", "mul_scalar_rwnaf"];
  simplify (addsimps [mul_scalar_rwnaf_1_equiv, mul_scalar_rwnaf_equiv] basic_ss);
  w4_unint_z3 ["scalar_get_bit"];
  });


p384_point_mul_ov <- llvm_verify m "ec_GFp_nistp384_point_mul"
  [ p384_point_add_jac_ov
  , p384_point_add_same_r_jac_ov
  , p384_point_add_same_l_jac_ov
  , p384_point_double_ov
  , p384_point_double_same_ov
  , bignum_fromlebytes_6_ov
  , bignum_tolebytes_6_ov
  , p384_felem_cmovznz_ov
  , p384_felem_cmovznz_same_l_ov
  , p384_felem_cmovznz_same_r_ov
  , p384_select_point_ov
  , p384_mul_scalar_rwnaf_ov
  , bignum_neg_p384_ov
  , p384_felem_copy_ov
  ]
  true
  p384_point_mul_spec
  (do{
  w4_unint_z3 ["select_point_ct", "felem_sqr", "felem_add", "felem_sub", 
  "felem_mul",
  "mul_scalar_rwnaf", "felem_opp",
   "select_point", 
  "felem_cmovznz",
  "felem_to_bytes", "felem_from_bytes"
  ];
  });



