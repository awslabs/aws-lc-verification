/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
*/

module EC_fiat_mul where

import EC_fiat_primitives

type wsize_max = 7

parameter
  type wsize : #

  type constraint (wsize > 0, wsize <= wsize_max)
  type constraint (bit_length%wsize!=0, bit_length/wsize>1)


type tsize = 2^^(wsize - 1)

type point_id = [16]


fiat_pre_comp_table : point -> [tsize]point
fiat_pre_comp_table p = scanl f p [1..(tsize-1)]
  where
    twoP = (fiat_point_double p)
    f z i  = (fiat_point_add 0 twoP z)


fiat_mul_scalar_rwnaf_loop_body_ct :
  scalar -> [16] -> [64] -> ([16], [16])

fiat_mul_scalar_rwnaf_loop_body_ct s window i = (d, window')
  where
    two_to_wsize = (1 << `wsize)
    wsize_mask = (1 << (`wsize + 1)) - 1
    d = ((window && (wsize_mask:[16])) - (two_to_wsize:[16]))
    // Not sure why this compiled to unsigned shift && some constant
    // constant is 0x1FFFFFF, so this just masks off the sign bit and other high order bits
    // This is correct because d <= window, so the difference is non-negative, and all these bits are 0
    window_0 = (((sign_extend_16_64 window) - (sign_extend_16_64 d)) >> (`wsize:[32])) && 0x0000000001FFFFFF
    i_wsize = (drop`{32} (i&&0x00000000000000FF)) * (`wsize:[32])
    window_7 = foldl f window_0 [1..7]
    f a j = a + ((fiat_get_bit s (i_wsize + ((`wsize:[32]) + j))) << j)
    window' = drop`{48} window_7

fiat_mul_scalar_rwnaf_ct : scalar -> [(bit_length / wsize) + 1]point_id
fiat_mul_scalar_rwnaf_ct in = out'
  where
    wsize_mask = (1 << (`wsize + 1)) - 1
    window = zero # (((drop`{bit_length-8} in) && (drop`{8} (wsize_mask:[16]))) || 1)
    f (d, w) = fiat_mul_scalar_rwnaf_loop_body_ct in w
    outls = scanl f (0, window) [0 .. ((bit_length/wsize) - 1)]
    window' = (outls!0).1
    out' = (drop`{1} ((map (\p -> p.0) outls)#[window']))

fiat_mul_scalar_rwnaf_loop_body_1 : 
  scalar -> [16] -> [64] -> ([16], [16])
fiat_mul_scalar_rwnaf_loop_body_1 s window i = (d, window')
  where
    two_to_wsize = (1 << `wsize)
    wsize_mask = (1 << (`wsize + 1)) - 1
    d = ((window && (wsize_mask:[16])) - (two_to_wsize:[16]))
    window_0 = (((sign_extend_16_64 window) - (sign_extend_16_64 d)) >> (`wsize:[32]))
    i_wsize = (drop`{32} (i&&0x00000000000000FF)) * (`wsize:[32])
    bit_0_pos = i_wsize + (`wsize:[32])
    window_7 = window_0 + ((drop (s>>bit_0_pos) && (0x00000000000000FE : [64])))
    window' = drop`{48} window_7


fiat_mul_scalar_rwnaf_1 : scalar -> [(bit_length / wsize) + 1]point_id
fiat_mul_scalar_rwnaf_1 in = out'
  where
    wsize_mask = (1 << (`wsize + 1)) - 1
    window = zero # (((drop`{bit_length-8} in) && (drop`{8} (wsize_mask:[16]))) || 1)
    f (d, w) = fiat_mul_scalar_rwnaf_loop_body_1 in w
    outls = scanl f (0, window) [0 .. ((bit_length/wsize) - 1)]
    window' = (outls!0).1
    out' = (drop`{1} ((map (\p -> p.0) outls)#[window']))

fiat_mul_scalar_rwnaf_odd_loop_body : 
  scalar -> ([16], scalar)
fiat_mul_scalar_rwnaf_odd_loop_body s = (drop d, s')
  where
    two_to_wsize = (1 << `wsize)
    d = ((s % (2 * two_to_wsize)) - two_to_wsize)
    s' = (s - d) >> `wsize

fiat_mul_scalar_rwnaf_odd : scalar -> [(bit_length / wsize) + 1]point_id
fiat_mul_scalar_rwnaf_odd in = out'
  where
    f (d, s) i = fiat_mul_scalar_rwnaf_odd_loop_body s
    outls = scanl f (fiat_mul_scalar_rwnaf_odd_loop_body in) [0 .. ((bit_length/wsize) - 2)]
    window' = (drop (outls!0).1)
    out' = ((map (\p -> p.0) outls)#[window'])

fiat_mul_scalar_rwnaf : scalar -> [(bit_length / wsize) + 1]point_id 
fiat_mul_scalar_rwnaf in = fiat_mul_scalar_rwnaf_odd (in || 1)

fiat_select_point : [64] -> [tsize]point -> point
fiat_select_point i t = t@i

fiat_select_point_loop_body : [64] -> point -> [64] -> point -> point
fiat_select_point_loop_body idx acc i p = [x, y, z]
  where
    mismatch = i ^ idx
    x = (fiat_field_cmovznz mismatch (p@0) (acc@0))
    y = (fiat_field_cmovznz mismatch (p@1) (acc@1))
    z = (fiat_field_cmovznz mismatch (p@2) (acc@2))

fiat_select_point_ct : [64] -> [tsize]point -> point
fiat_select_point_ct idx t = foldl f def (zip [0..(tsize-1)] t)
    where
      def = [zero_felem, zero_felem, zero_felem]
      f acc (i, p) = fiat_select_point_loop_body idx acc i p

point_id_to_limb : point_id -> limb
point_id_to_limb x = (0:[48]) # (x)

fiat_double_add_body : [tsize]point -> point -> point_id -> point
fiat_double_add_body t p d = res
  where
    doubled = foldl (\x -> \y -> fiat_point_double x) p [0 .. (wsize - 1)]
    is_neg = (d >> 7) && 1
    abs_d = (d ^ (-is_neg)) + is_neg
    idx = abs_d >>$ 1
    tmp = (fiat_select_point_ct (sign_extend_16_64 idx) t)
    ftmp = fiat_field_opp (tmp@1)
    r = [tmp@0, (fiat_field_cmovznz (point_id_to_limb is_neg) (tmp@1) ftmp), (tmp@2)]
    res = fiat_point_add 0 doubled r

fiat_point_mul : point -> scalar -> point
fiat_point_mul p s = r
  where
    pre_comp = fiat_pre_comp_table p
    rnaf = fiat_mul_scalar_rwnaf s
    idx = ((rnaf@(`bit_length/`wsize)) >>$ 1)
    acc = (fiat_select_point_ct (sign_extend_16_64 idx) pre_comp)
    res = foldl (fiat_double_add_body pre_comp) acc (drop `{1} (reverse rnaf))
    r = conditional_subtract_if_even_ct res s (pre_comp@0)

fiat_point_mul_generic : JacobianBVPoint -> scalar -> JacobianBVPoint
fiat_point_mul_generic p s = r
  where
    px = fiat_from_bytes p.X
    py = fiat_from_bytes p.Y
    pz = fiat_from_bytes p.Z
    r_fiat = (fiat_point_mul [px, py, pz] s)
    r = {X = fiat_to_bytes (r_fiat@0), Y = fiat_to_bytes (r_fiat@1), Z = fiat_to_bytes (r_fiat@2)}
